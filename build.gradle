plugins {
    id 'java-library'
    id 'com.gradleup.shadow' version '9.3.1'
    id 'maven-publish'
}

group = 'me.thezombiepl.plugin'

def getPluginVersion() {
    def pluginFile = file('src/main/resources/plugin.yml')
    if (!pluginFile.exists()) return '1.0.0'
    def text = pluginFile.text
    def matcher = (text =~ /(?m)^\s*version\s*:\s*(.+)\s*$/)
    if (matcher.find()) {
        return matcher.group(1).trim()
    }
    return '1.0.0'
}

version = getPluginVersion()
description = 'Core utilities plugin - MC 1.8-1.21 (shared Adventure API)'

repositories {
    mavenCentral()
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
}

dependencies {
	// Paper API
    compileOnly 'com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT'
    
    // Velocity API
    compileOnly 'com.velocitypowered:velocity-api:3.4.0-SNAPSHOT'
    annotationProcessor 'com.velocitypowered:velocity-api:3.4.0-SNAPSHOT'

    // Adventure (Colors, Components)
    api 'net.kyori:adventure-api:4.26.1'
    api 'net.kyori:adventure-text-minimessage:4.26.1'
    api 'net.kyori:adventure-text-serializer-legacy:4.26.1'

	// Adventure Platform Bukkit (Required for 1.8-1.15)
    api 'net.kyori:adventure-platform-bukkit:4.4.1'
    
    api 'dev.dejvokep:boosted-yaml:1.3.7'
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
    withSourcesJar()
    withJavadocJar()
}
configurations.compileClasspath {
    attributes {
        attribute(TargetJvmVersion.TARGET_JVM_VERSION_ATTRIBUTE, 21)
    }
}

tasks.withType(JavaCompile) {
    options.release = 8
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.encoding = 'UTF-8'
    failOnError = false
}

shadowJar {
    archiveBaseName = 'ZCore'
    archiveClassifier = ''

    // MERGE SERVICE FILES (Adventure uses services)
    mergeServiceFiles()

    // REMOVE DUPLICATES
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    // DELETE UNNECESSARY FILES
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/*.txt'
    exclude 'module-info.class'
    exclude 'META-INF/versions/**'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = 'zcore'
            version = project.version

            artifact(shadowJar) {
                classifier = null
            }
        }
    }
}


tasks.jar.enabled = false
tasks.build.dependsOn shadowJar