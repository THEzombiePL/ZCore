plugins {
    id 'java-library'
    id 'com.gradleup.shadow' version '8.3.9'
    id 'maven-publish'
}

group = 'me.thezombiepl.plugin'

def getPluginVersion() {
    def pluginFile = file('src/main/resources/plugin.yml')
    if (!pluginFile.exists()) return '1.0.0'
    def text = pluginFile.text
    def matcher = (text =~ /(?m)^\s*version\s*:\s*(.+)\s*$/)
    if (matcher.find()) {
        return matcher.group(1).trim()
    }
    return '1.0.0'
}

version = getPluginVersion()
description = 'Core utilities plugin - MC 1.8-1.21 (shared Adventure API)'

repositories {
    mavenCentral()
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
}

dependencies {
    compileOnly 'com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT'
    
    api 'net.kyori:adventure-api:4.17.0'
    api 'net.kyori:adventure-text-minimessage:4.17.0'
    api 'net.kyori:adventure-text-serializer-legacy:4.17.0'
    api 'net.kyori:adventure-platform-bukkit:4.3.4'
    
    api 'dev.dejvokep:boosted-yaml:1.3.7'
    api 'org.json:json:20240303'
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile) {
    options.release = 8
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.encoding = 'UTF-8'
    failOnError = false
}

shadowJar {
    archiveBaseName = 'ZCore'
    archiveClassifier = ''
    
    exclude 'META-INF/**'
    exclude '**/*.kotlin_*'
    exclude 'module-info.class'
    
    mergeServiceFiles()
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'me.thezombiepl.plugin'
            artifactId = 'zcore'
            version = project.version
            from components.java
			artifact(shadowJar) {
                classifier = ''
            }
        }
    }
}

tasks.jar.enabled = false
tasks.build.dependsOn shadowJar